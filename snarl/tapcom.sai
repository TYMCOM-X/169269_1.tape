ENTRY;
BEGIN "TAPCOM"

REQUIRE "(sailib)sail.def" SOURCE!FILE;
REQUIRE "sndsn.sai" SOURCE!FILE;
ck!sn
REQUIRE "snsym.sai" SOURCE!FILE;
elsec
REQUIRE "dsnsym.sai" SOURCE!FILE;
endc
REQUIRE "sndefs.sai" SOURCE!FILE;
!get!module (<extend>);

EXTERNAL STRING snstat;				\ SNARL or DSNARL status vector;

ck!sn
EXTERNAL INTEGER ARRAY tapblk[1:tape!buf!size];	\ For holding a whole physical tape record.;
endc

EXTERNAL INTEGER mtchan, mteof,			\ mag tape channel stuff;
   mtstat,					\ sense data,,status bits;
   mtdens,					\ currently set density;
   mtrec,					\ physical record containing beginning of current tape file;
   mtpos,					\ position number of last complete file processed;
   mtibuf,					\ number of buffers for buffered input;
   mtobuf,					\ number of buffers for buffered input;
   tappfno,					\ current tape physical file number;
   tapbloc,					\ current tape physical record number;
   cmdtkn;					\ token value for command in process;

EXTERNAL STRING mtdev,				\ mag tape device name;
   mtfile;					\ name of last file accessed;

EXTERNAL BOOLEAN dskdev;			\ True if current mag tape device is really a disk;

ck!sn
EXTERNAL INTEGER ree!sw;			\ REELSET switch value;

EXTERNAL BOOLEAN stor!sw,			\ true if last file accessed was stored, false if retrieved;
   new!sw,					\ True if NEW TAPE command just done;
   app!sw;					\ True if APPEND command just done;

EXTERNAL STRING PROCEDURE curdt (REFERENCE INTEGER time, date);
EXTERNAL STRING PROCEDURE pr!time (INTEGER time);
EXTERNAL STRING PROCEDURE pr!date (INTEGER date);
EXTERNAL STRING PROCEDURE pr!reel (INTEGER reelno);
endc
EXTERNAL BOOLEAN PROCEDURE readint (STRING prompt; INTEGER rule;
   REFERENCE INTEGER result);
EXTERNAL BOOLEAN PROCEDURE readstr (STRING prompt; INTEGER rule);
EXTERNAL INTEGER PROCEDURE readkey (STRING prompt; INTEGER rule);
EXTERNAL BOOLEAN PROCEDURE set!tape!bufs (INTEGER direction);
EXTERNAL INTEGER PROCEDURE relchan (REFERENCE INTEGER channel;
   INTEGER closebits(0));
EXTERNAL BOOLEAN PROCEDURE checrec;
EXTERNAL BOOLEAN PROCEDURE xeof( INTEGER eofvar );
EXTERNAL BOOLEAN PROCEDURE xerr( INTEGER eofvar );
EXTERNAL PROCEDURE s!ret( INTEGER comnd, lsttyp, afttim(-1),
   beftim('377777777777), modbts(0);  STRING stusr(null), stnam(null),
   stext(null), endusr(null), endnam(null), endext(null) );

PRELOAD!WITH 6250,1600,800,556,200;
INTEGER ARRAY dens[1:5];
INTEGER ARRAY dposbl[1:3];
INTEGER ARRAY addr[1:2];
INTEGER ARRAY taphed[1:17];

INTERNAL PROCEDURE s!rewi (BOOLEAN wait);
	\ This procedure is the service routine for the REWIND command.  If
the "wait" parameter is "true", then "s!rewi" will wait for completion
before returning.;
BEGIN
   if dskdev then
   BEGIN
      useti(mtchan,1);  useto(mtchan,1);
   END
   else BEGIN
      mtape(mtchan,"W");  if wait then mtape(mtchan,null);
   END;
   tappfno_ 1;  tapbloc_ -1;  ck!sn app!sw_ (new!sw_ false); endc
   return;
END;


INTERNAL PROCEDURE s!unlo (BOOLEAN wait);
	\ This procedure is the service routine for the REWIND command.  If
the "wait" parameter is "true", then "s!rewi" will wait for completion
before returning.;
BEGIN
   ifc debug thenc s!rewi(wait)
   elsec  mtape(mtchan,"U");  if wait then mtape(mtchan,null);  endc;
   tappfno_ 1;  tapbloc_ -1;  ck!sn app!sw_ (new!sw_ false); endc
   return;
END;
ck!sn INTERNAL BOOLEAN PROCEDURE chk!hed;
	\ This procedure reads the first physical block on the tape
currently mounted and returns "true" if the block seems to be a legal
SNARL header block.  If a SNARL header, the pertinent information about
the tape is printed to the current TTY device.  If not a SNARL header, a
message is printed to indicate that fact.  "Chk!hed" release and re-opens
the tape device in dump mode.
;
BEGIN
   INTEGER timval, datval;
   BOOLEAN snarl!sw;

   snarl!sw_ false;
   if mtchan neq -1 then relchan(mtchan);  mtchan_ getchan;
   open(mtchan,mtdev,'760017,0,0,0,mteof,mteof);			\ Make sure and set unbuffered mode;
   if mteof then
   BEGIN
      print("Cannot open device """ & mtdev & """." & crlf);
      relchan(mtchan);  return (false);
   END;
   if dskdev then
   BEGIN
      lookup(mtchan,"snarl.tap",mteof); enter(mtchan,"snarl.tap",mteof);
   END;
   s!rewi(true);  arrclr(tapblk,0);					\ Rewind;
   arryin(mtchan,tapblk[1],tape!buf!size);				\ Read 1st tape record;
   if ((tapblk[1] land '777777000000) = hed) and			\ Check to see if SNARL tape.;
      (tapblk[2] = cvasc("SNARL")) then
   BEGIN
      timval_ tapblk[8];  datval_ tapblk[9];
      gmt!to!user(<datval>,<timval>);					\ If so, print some information;
      print("This SNARL tape #" & pr!reel(tapblk[7]));			\ about the tape;
      print(" was created on " & cvstr(tapblk[13]) & cvstr(tapblk[14]));
      print(" at " & pr!time(timval) & " on ");
      print(pr!date(datval) & "." & crlf);
      snarl!sw_ true;
   END;
   if (mteof land '700000000000) then
   BEGIN
      print("Tape read error - density may differ from current setting."
	 & crlf);
      return (false);
   END;
   if not snarl!sw then
   BEGIN
      print("This tape is not a SNARL tape." & crlf);
      return (false);
   END
   else return (true);
END;
endc
INTERNAL PROCEDURE s!devi (STRING devnam(null); INTEGER densty(0));
	\ This procedure is the service routine for the DEVICE
command.  "Devnam" is the device name, if supplied, and "densty" is the
desired density, if supplied.  The tape device is opened in dump mode.
;
BEGIN
   INTEGER devchr!wd,temp,i,j;  STRING str;
   dskdev_ false;  devchr!wd_calli(cvsix(devnam),devchr);
   IFC not production!version THENC
   IF (temp_devchr!wd land (dv!mta+dv!dsk))=0 THEN
      print("""" & devnam & """ is not a legal device." & crlf)
   ELSEC
   IF (temp_devchr!wd land dv!mta)=0 THEN
      print("""" & devnam & """ is not a legal magtape device." & crlf)
   ENDC
   ELSE IF (devchr!wd land dv!avl)=0 THEN
      print("""" & devnam & """ is not available" & crlf);
   IF temp=0 THEN RETURN;
   if mtchan neq -1 then relchan(mtchan);  mtchan_ getchan;
   open (mtchan,devnam,'760017,0,0,0,mteof,mteof);
   if mteof then
   BEGIN
      print("Cannot open device """ & devnam & """." & crlf);
      relchan(mtchan);  return;
   END;
   if temp=dv!dsk then
   BEGIN
      lookup( mtchan, "snarl.tap", mteof);
      enter( mtchan, "snarl.tap", mteof);
      dskdev_ true;  return;
   END;
   addr[1]_!ugsts;  temp_chnior(mtchan,addr[1],!chugf);
   mtstat_addr[2];  temp_mtstat lsh 12;  j_3;
   FOR i_1 step 1 until 5 DO
      IF (temp_temp lsh 1)<0 THEN BEGIN
	 dposbl[j]_dens[i];  j_j-1;
      END;
   j_ 3-j;
   if dposbl[1]=0 then
   BEGIN
      dposbl[1]_dposbl[2];  dposbl[2]_0;
   END;
   if densty then							\ If a density has been specified;
      for i_ 1 step 1 until 3 do					\ do not interact with user.;
      BEGIN
	 if dposbl[i] neq densty then continue;				\ Check supplied density;
	 mtdens_ densty;  done;						\ for correctness;
      END
   else BEGIN
      temp_false;
      print("TRACKS:  ",(IF (mtstat lsh 12)<0 THEN 7 ELSE 9),crlf);
      if j=1 then print("DENSITY:  " &					\ If only one density is possible;
	 cvs(mtdens_ dposbl[3]) & crlf)					\ then report it and use it.;
      else BEGIN
	 str_"DENSITY (" & cvs(dposbl[1]) & ",";
	 if j=3 then str_str & cvs(dposbl[2]) & ",";
 	 str_str & cvs(dposbl[3]) & ")  >>";
	 temp_false;
	 while not temp do BEGIN
            temp_readint(str,!dens,densty);
            if not (temp and densty) then continue;
            temp_false;
            for i_1 step 1 until 3 do
	    BEGIN
	       if dposbl[i] neq densty then continue;
	       temp_true;  mtdens_ densty;  done;
            END;
         END;
      END;
   END;

\  Set the selected tape density.  DEC mode is the default.
\  Then get tape status bits and save.;

   setsts(mtchan,(getsts(mtchan) lor (i lsh 7)));
   if mtstat geq 0 then print("TAPE NOT WRITE PROTECTED." & crlf);
   temp_chnior(mtchan,addr[1],!chugf);  mtstat_addr[2];
   return;
END;
ck!sn INTERNAL SIMPLE PROCEDURE s!wher;
	\ This is the service routine for the WHERE command.  It reports
the latest access information about the current tape using the status
information in global storage.
;
BEGIN
   if length(mtfile) and ( (stor!sw and cmdtkn=!stor!c) or 
      (not stor!sw and cmdtkn=!retr!c) ) then
   BEGIN
      print("The last complete file " & (if stor!sw then "written to"
	 else "read from") & " tape was" & crlf);
      print(mtfile & " at file position ",mtpos,"." & crlf);
   END
   else print("No files processed by the current command." & crlf);
   return;
END;
endc
ck!sn STRING PROCEDURE get!tid;
	\ This procedure gets a TID value from the user for verification.;
return (readstr("Please enter the tape TID for verification.  >>",!tidval));
endc
ck!sn INTERNAL PROCEDURE s!new (STRING tidval(null));
	\ This procedure is the service routine for the NEW TAPE
command.  The "tidval" parameter, if supplied, is the value to be stored
on the tape.
;
BEGIN
   BOOLEAN snarl!sw, noconf!sw;  STRING str;
   INTEGER tid, verif!tid, timval, datval, t;

PROCEDURE create!header;
	\ This routine creates or re-initializes the header record of a
SNARL tape and makes room for the directory blocks which will follow.
;
BEGIN
   INTEGER i;
   if not set!tape!bufs(direct_ "O") then return;
   taphed[1]_ hed+17;  taphed[2]_ cvasc("SNARL");			\ Identify the tape as SNARL tape;
   taphed[3]_ memory['137];  taphed[4]_ 0;				\ SNARL version and tape version;
   if tidval neq null then
   BEGIN
      taphed[5]_ cvasc(tidval[1 for 5]);				\ Put in new TID if supplied.;
      taphed[6]_ cvasc(tidval[6 for 5]);
   END
   else if not snarl!sw then taphed[5]_ (taphed[6]_ 0);
   taphed[7]_ (ree!sw lsh 18) + 1;					\ Reel number.;
   curdt(taphed[8],taphed[9]);  timval_ taphed[8];  datval_ taphed[9];	\ Save current time and date;
   user!to!gmt(<taphed[9]>,<taphed[8]>);				\ Current date and time in GMT;
   taphed[10]_ 0;  taphed[11]_ mtdens;  taphed[12]_ tape!buf!size;	\ Tape type, density, record size;
   for i_ 0 step 1 until 4 do						\ Get 5 words of system;
      taphed[13+i]_ calli((i lsh 18)+'11,'41);
   arrclr(tapblk);  arrtran(tapblk,taphed);
   tapblk[18]_ dum+tape!buf!size-17;
   s!rewi(true);							\ Rewind and wait;
   arryout(mtchan,tapblk[1],tape!buf!size);				\ a full tape block;
   if not dskdev then for i_ 1 step 1 until 3 do
   BEGIN
      mtape(mtchan,"S");  mtape(mtchan,null);				\ Put out some blank tape.;
   END;
   arrclr(tapblk,check!wd);						\ Put out a check record.;
   arryout(mtchan,tapblk[1],tape!buf!size);  arrclr(tapblk);		\ Put out 10 blank records;
   for i_ 1 step 1 until 10 do 
      arryout(mtchan,tapblk[1],tape!buf!size);				\ to reserve directory room.;
   if not dskdev then for i_ 1 step 1 until 10 do
   BEGIN
      mtape(mtchan,"S");  mtape(mtchan,null);				\ Put out some blank tape.;
   END;
   arrclr(tapblk,check!wd);
   arryout(mtchan,tapblk[1],tape!buf!size);				\ Put out a check record.;
   if not dskdev then close(mtchan);					\ Put out an "eof" if tape;
   tappfno_ 2;  tapbloc_ 0;
   snstat_ "C" & snstat;  new!sw_ true;
   print("Tape #" & pr!reel(taphed[7]) & " created at " &
      pr!time(timval) & " on " & pr!date(datval) & " with ");
   if not taphed[5] then print ("no TID.")
   else print ("TID """ & cvastr(taphed[5]) & cvastr(taphed[6]) & """.");
   print(crlf);
   return
END;
endc
ck!sn  \ Body of "s!new" starts here;

   if mtchan = -1 then
   BEGIN
      print("Tape device must be initialized first." & crlf);
      return;
   END;
   if (snarl!sw_ chk!hed) then tid_ cvastr(tapblk[5])&cvastr(tapblk[6])	\ Read header, check if SNARL tape, and print info.;
   else tid_ null;
   noconf!sw_ false;
   if tidval=null then							\ Go through the TID;
   BEGIN
      if tid=null then
      BEGIN
	 str_ "Initializing with no TID assigned.  ";
	 if snarl!sw then str_ "Re-" & str;
	 print(str);
      END
      else BEGIN
	 verif!tid_ get!tid;
	 if not equ(verif!tid,tid) then
	    print("TID on tape is """ & tid & """.  ");
	 print("Re-initializing.  ");
      END;
   END
   else BEGIN
      if tid=null then
      BEGIN
	 str_ "Initializing tape with TID " & tidval &
	    " assigned for the first time." & crlf;
	 if snarl!sw then str_ "Re-" & str;
	 print(str);
      END
      else BEGIN
	 if not equ(tidval,tid) then
	    print("Tape TID is """ & tid &
	       """.  Re-initializing with TID """ &
	       tidval & """." & crlf)
	 else BEGIN
	    print ("Re-initializing.  ");  noconf!sw_ true;
	 END;
      END;
   END;
   if noconf!sw then create!header
   else if readkey("OK to continue?  >>",!yes!or!no) = !yes
      then create!header
   else snstat_ "A" & snstat;
   return;
END;
endc
INTEGER PROCEDURE taperec( REFERENCE INTEGER ARRAY tapedata;
   INTEGER stpnt, trectyp );
	\ "Taperec" searches the buffer "tapedata" starting with the
array index "stpnt" for the tape record header in "trectyp".  The
buffer index is returned if the record is found, and 0 is returned
otherwise.
;
BEGIN
   integer ub, z;
   ub_ arrinfo( tapedata, 2 );
   for z_ stpnt step 1 until ub do
      if !lh( tapedata[z] ) = !lh( trectyp ) and
	( z + !rh(<tapedata[z]>) ) leq (ub+1) then return( z );
   return( 0 );
END;
ck!sn INTEGER PROCEDURE findrec( INTEGER stpnt, trectyp );
	\ "Findrec" searches tape records for the first occurrence of
the record denoted by "trectyp".  It is intended to be used with
unbuffered I/O.  Before, calling "findrec", the first buffer to be
searched should be in "tapblk".  "Stpnt" is the buffer index where the
search will begin.  New records are read as needed and "tapbloc" is
updated.  The index into "tapblk" is returned if successful.  A zero is
returned if unsuccessful.
;
BEGIN
   integer x;
   while not ( xeof(mteof) or xerr(mteof) ) do
   BEGIN
      if ( x_ taperec(tapblk,stpnt,trectyp) ) then return( x );
      arryin( mtchan, tapblk[1], tape!buf!size );
      stpnt_ 1;
      if (tapbloc+1) neq tapblk[2] then
      BEGIN
	 print( "Tape consistency check failed." & crlf );
	 return( 0 );
      END;
      tapbloc_ tapblk[2];
   END;
   return( 0 );
END;
endc
ck!sn INTERNAL PROCEDURE s!appe( STRING tidval(null) );
	\ This procedure is the service routine for the APPEND command.
;
BEGIN
   integer tid, verif!tid, x;
   string modsav;
   boolean snarl!sw, noconf!sw;

PROCEDURE modify!header;
	\ This routine modifies the TID in the header record of a SNARL
tape.  It is assumed that the header just read is in the array "tapblk".
;
BEGIN
   tapblk[5]_ cvasc( tidval[1 for 5] );
   tapblk[6]_ cvasc( tidval[6 for 5] );
   tapblk[18]_ dum+tape!buf!size-17;
   s!rewi(true);
   arryout(mtchan,tapblk[1],tape!buf!size);
   if not dskdev then mtape(mtchan,"S");
   arryin( mtchan, tapblk[1], tape!buf!size);
   if not checrec then
   BEGIN
      print( "Tape header check failed.  Time to rewrite" & crlf );
      print( "this tape using DSNARL." & crlf );
   END;
   return;
END;
\  Body of "s!appe" starts here;

   if mtchan = -1 then
   BEGIN
      print("Tape device must be initialized first." & crlf);
      return;
   END;
   s!devi( mtdev, mtdens );
   if (snarl!sw_ chk!hed) then tid_ cvastr(tapblk[5])&cvastr(tapblk[6])	\ Read header, check if SNARL tape, and print info.;
   else BEGIN
      print( "Please use ""NEW TAPE"" to re-initialize." & crlf );
      return;
   END;
   noconf!sw_ false;
   if tidval=null then							\ Go through the TID;
   BEGIN
      if tid=null then
	 print( "Preparing to append.  " )
      else BEGIN
	 verif!tid_ get!tid;
	 if not equ(verif!tid,tid) then
	    print("TID on tape is """ & tid & """.  ");
	 print( "Preparing to append.  " );
      END;
   END
   else BEGIN
      if tid=null then
	 print( "Setting TID to """ & tidval & """" & crlf )
      else BEGIN
	 if not equ(tidval,tid) then
	    print("Tape TID is """ & tid &
	       """.  Resetting TID to """ & tidval & """." & crlf)
	 else noconf!sw_ true;
	 print( "Preparing to append.  ");
      END;
   END;

   if not noconf!sw then
      if readkey( "OK to continue?  >>", !yes!or!no ) = !no then return
      else if length( tidval ) then modify!header;
   modsav_ getprint;  setprint( null, "I" );
   s!ret( !dire!c, !allst );
   setprint( null, modsav );  s!devi( mtdev, mtdens );
   if dskdev then useti( mtchan, (12+tapbloc)*dsk!index+1 )
   else BEGIN
      mtape( mtchan, "R" );  mtape( mtchan, "R" );
   END;
   arryin( mtchan, tapblk[1], tape!buf!size );
   tapbloc_ tapblk[2];
   if not ( x_ findrec( 1, eot) ) then
   BEGIN
      print( "Logical end-of-tape marker not found." & crlf );
      print( "Please do a ""DIRECTORY"" and ""CUT AFTER"" to recover."
	 & crlf );
      return;
   END;
   tapblk[x]_ dum + tape!buf!size - x + 1;
   if dskdev then useto( mtchan, (12+tapbloc)*dsk!index+1 )
   else mtape( mtchan, "R" );
   arryout( mtchan, tapblk[1], tape!buf!size );
   if not set!tape!bufs( "O" ) then return;
   if dskdev then useto( mtchan, (13+tapbloc)*dsk!index+1 )
   else mtape( mtchan, "B" );
   app!sw_ true;  snstat_ "C" & snstat;  return;
END;
endc
ck!sn INTERNAL PROCEDURE s!cut( INTEGER filpos );
	\ This procedure is the service routine to the CUT AFTER
command.
;
BEGIN
   integer userppn, cutbloc, cutword, x, y, z;
   boolean sdirbad;

INTEGER PROCEDURE srchpos( INTEGER filpos );
	\ This procedure finds the beginning of the file on tape whose
position is "filpos", using "findrec" to do this.  The index into
"tapblk" of the "lok" record is returned if successful.  A zero is
returned if unsuccessful.  "Srchpos" is meant to be used with
unbuffered I/O.
;
BEGIN
   integer x;
   print( "Could not locate cut point using on-tape directory." );
   print( crlf & "Doing full tape search." & crlf );
   print( "On-tape directory may be bad." & crlf );
   arryin( mtchan, tapblk[1], tape!buf!size );
   tapbloc_ tapblk[2];  x_ 1;
   while true do
   BEGIN
      if not ( x_ findrec(x,lok) ) then return( 0 );
      if tapblk[x+1] = filpos then return( x );
   END;
END;
\  Body of "s!cut" starts here.;

   if mtchan = -1 then
   BEGIN
      print( "Tape device must be initialized first." & crlf );
      return;
   END;
   s!devi( mtdev, mtdens );
   if not dskdev then
   BEGIN
      s!rewi( true );
      arryin( mtchan, tapblk[1], tape!buf!size );
      arryin( mtchan, tapblk[1], tape!buf!size );
   END
   else useti( mtchan, 2*dsk!index+1 );
   sdirbad_ cutbloc_ cutword_ tapbloc_ 0;
   for x_ 1 step 1 until num!dir!blocks do
   BEGIN "search tape directory"
      integer frstent, lastent;
      arryin( mtchan, tapblk[1], tape!buf!size );
      tapbloc_ tapbloc + 1;
      frstent_ 3;  lastent_ 1 + !rh(tapblk[1]) - dir!index;
      if (lastent > tape!buf!size) or (lastent leq 0) then
      BEGIN
	 sdirbad_ true;  done "search tape directory";
      END;
      if filpos > tapblk[lastent] then continue "search tape directory";
      for y_ frstent step dir!index until lastent do
	 if filpos = tapblk[y] then
	 BEGIN
	    cutbloc_ tapblk[y+1];  cutword_ tapblk[y+2];
	    done "search tape directory";
	 END;
   END "search tape directory";
   if not cutbloc then sdirbad_ true;
   if not sdirbad then
   BEGIN
      if dskdev then useto( mtchan, (tapbloc+1)*dsk!index+1 )
      else mtape( mtchan, "R" );
      tapblk[1]_ dir + (y_ y+dir!index) - 1;
      for z_ y step 1 until tape!buf!size do tapblk[z]_ 0;
      arryout( mtchan, tapblk[1], tape!buf!size );
      arrclr( tapblk, 0 );
      while tapbloc < num!dir!blocks do
      BEGIN
	 arryout( mtchan, tapblk[1], tape!buf!size );
	 tapbloc_ tapbloc + 1;
      END;
      if not dskdev then mtape( mtchan, "S" )
      else useti( mtchan, (tapbloc+2)*dsk!index+1 );
      arryin( mtchan, tapblk[1], tape!buf!size );
      if not checrec then
      BEGIN
	 print( "Directory limit check failed.  Time to" & crlf );
	 print( "re-write this tape using DSNARL." & crlf );
      END;
   END;
   if sdirbad then cutword_ srchpos( filpos )
   else BEGIN
      if dskdev then useti( mtchan, (12+cutbloc)*dsk!index+1 )
      else BEGIN
	 mtape( mtchan, "A" );  x_ cutbloc - 1;
	 for y_ 1 step 1 until x do mtape( mtchan, "F" );
      END;
      arryin( mtchan, tapblk[1], tape!buf!size );
      tapbloc_ tapblk[2];
      if !lh( <tapblk[cutword]> ) neq !lh( <lok> ) then sdirbad_ true
      else if tapblk[cutword+1] neq filpos then sdirbad_ true;
      if sdirbad then
      BEGIN
	 if dskdev then useti( mtchan, 13*dsk!index+1 )
	 else BEGIN
	    mtape( mtchan, "W" );  mtape( mtchan, null );
	    mtape( mtchan, "A" );  mtape( mtchan, "A" );
	 END;
	 cutword_ srchpos( filpos );
      END;
   END;
   if cutword then
   BEGIN
      userppn_ tapblk[cutword+5+!rbppn];
      cutword_ findrec( cutword, feo );
      cutword_ cutword + !rh( <tapblk[cutword]> );
   END;
   if not cutword then
   BEGIN
      print( "Could not find the requested cut point." & crlf);
      return;
   END;
   if dskdev then useto( mtchan, (12+tapbloc)*dsk!index+1 )
   else mtape( mtchan, "R" );
   if cutword > (tape!buf!size-3) then
   BEGIN
      tapblk[cutword]_ dum + tape!buf!size - cutword + 1;
      arryout( mtchan, tapblk[1], tape!buf!size );
      tapblk[1]_ rcd + 4;  tapblk[2]_ (tapbloc_ tapbloc + 1);
      tapblk[3]_ tapblk[4]_ 0;
      cutword_ 5;
   END;
   tapblk[cutword]_ eou + 2;
   tapblk[cutword+1]_ userppn;
   tapblk[cutword+2]_ eot + 1;
   tapblk[cutword+3]_ dum + tape!buf!size - cutword - 2;
   arryout( mtchan, tapblk[1], tape!buf!size );
   if dskdev then chniov( mtchan, (tapbloc+13)*dsk!index*128, !chftr );
   close( mtchan);  snstat_ "C" & snstat;  return;
END;
endc


END "TAPCOM";
   -Yf®