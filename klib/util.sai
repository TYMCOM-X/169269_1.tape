entry Suppress.Echo, Restore.Echo, Prompt.User, Substr, Starts.With,
      Blanks, Indent.Width, Count.Lfs, Scan.Token, Is.Numeric,
      Deposit.Str, Clean, Ask.User, Form.Str, GetTab, Parse.File, Run,
      Logged.In.User.Name, Gfd.User.Name, Ctr.Str, Rpt.Str, Ceiling,
      RescanCommandLine ;

define ! = "comment", !! = "comment";
!! title Module Util ;
! *************************************************************************
************************************+**************************************
*                                                                         *
*                                                                         *
*                                 Util                                    *
*                                                                         *
*                                                                         *
************************************+**************************************


	A set of generally usable procedures and functions which should
	be maintained independently.


**************************************************************************;


begin "Util"
  require "(KLib)Sail.Hdr"   source!file;
  require "(KLib)Brktbl.Req" source!file;


  redefine
    $F.ASCII         = '0,
    $F.ASCII.LINE    = '1,
    $F.KATAKANA      = '2,
    $F.IMAGE         = '10,
    $F.IMAGE.BINARY  = '13,
    $F.BINARY        = '14,
    $F.DUMP.RECORDS  = '16,
    $F.DUMP          = '17;

      ! Tymcom-X Data Modes - borrowed from Files.Da;



! ************************************************************************;
!! title Rtn Suppress.Echo ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			     Rtn Suppress.Echo				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Suppress echo-on-input to the terminal.


==========================================================================;


  internal simple procedure Suppress.Echo;
  begin "Suppress.Echo"
    start!code
      hrroi	1,'64;
      auxcal	1, '200;
    end;
  end "Suppress.Echo";


! ===================================+=====================================
==========================================================================;
!! title Rtn Restore.Echo ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			     Rtn Restore.Echo				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Restore echo-on-input to the terminal.


==========================================================================;


  internal simple procedure Restore.Echo;
  begin "Restore.Echo"
    start!code
      hrroi	1, '64;
      auxcal	1, 0;
    end;
  end "Restore.Echo";


! ===================================+=====================================
==========================================================================;
!! title Rtn Prompt.User ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                            Rtn Prompt.User                              =
=                                                                         =
=                                                                         =
====================================+======================================


	Prompt the terminal for a file name, and return the string
	provided.  If Silently is set, then suppress echo.

	Note that, irrespective of Silently, the prompt will end
	with a newline.


==========================================================================;


  internal simple string procedure Prompt.User (string Prompt;
      boolean Silently(false));

  begin "Prompt.User"
    string
      Str;


    print(Prompt);

    if (Silently) then
      Suppress.Echo;

    Str _ inchwl;

    if (Silently) then
    begin
      Restore.Echo;
      print(crlf);
    end;

    return(Str);
  end "Prompt.User";


! ==================================+======================================
==========================================================================;
!! title Rtn Substr ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=				Rtn Substr				  =
=                                                                         =
=                                                                         =
====================================+======================================


	Return true if, regardless of case, one string is a left-
	substring of the other (with a length >= Min.Len).


==========================================================================;


  internal simple boolean procedure Substr (string Str1, Str2;
      integer Min.Len(0));

  begin "Substr"
    return(length(Str1) and length(Str2) and
	kequ(Str1[1 for (length(Str2) max Min.Len)],
	Str2[1 for (length(Str1) max Min.Len)]));
  end "Substr";


! ==================================+======================================
==========================================================================;
!! title Rtn Starts.With ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=				Rtn Starts.With				  =
=                                                                         =
=                                                                         =
====================================+======================================


	Return true if, regardless of case, Str2 string is a left-
	substring of Str1.


==========================================================================;


  internal simple boolean procedure Starts.With (string Str1, Str2);
  begin "Starts.With"
    return(length(Str1) and length(Str2) and
	kequ(Str1[1 for length(Str2)], Str2));
  end "Starts.With";


! ==================================+======================================
==========================================================================;
!! title Rtn Blanks ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                              Rtn Blanks                                 =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  internal simple string procedure Blanks (integer N);
  begin "Blanks"
    own string Blankstr;


    while (length(Blankstr) < N) do
      Blankstr _ Blankstr & " " & Blankstr;

    return(Blankstr[1 for N]);
  end "Blanks";


! ==================================+======================================
==========================================================================;
!! title SubRtn Indent.Width ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                          SubRtn Indent.Width                            =
=                                                                         =
=                                                                         =
===============================+======================================


	! <<  Rework using Brktbls.


==========================================================================;


  internal simple integer procedure Indent.Width (string X);
  begin "Indent.Width"
    integer I;


    I _ 1;

    while (equ(X[I for 1], sp)) do
      inc(I);

    return(I - 1);
  end "Indent.Width";


! ==================================+======================================
==========================================================================;
!! title Rtn Count.Lfs ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                             Rtn Count.Lfs                               =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  internal simple integer procedure Count.Lfs (string Str);
  begin "Count.Lfs"
    integer Brk;
    integer I;


    I _ 0;

    while (length(Str)) do
    begin
      scan(Str, Cpy.Tbl, Brk);

      if (Brk = lf) then
	inc(I);
    end;

    return(I);
  end "Count.Lfs";


! ==================================+======================================
==========================================================================;
!! title Rtn Scan.Token ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			      Rtn Scan.Token				  =
=                                                                         =
=                                                                         =
====================================+======================================


	Note:  An analogue of this routine (named Input.Token()) resides
	in FILES.DA.


	! <<  What about returning Brk?


==========================================================================;


  internal simple string procedure Scan.Token (reference string Str);
  begin "Scan.Token"
    integer Brk;


    return(scan(Str, Tok.Tbl, Brk));
  end "Scan.Token";


! ==================================+======================================
==========================================================================;
!! title Rtn Is.Numeric ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                             Rtn Is.Numeric                              =
=                                                                         =
=                                                                         =
====================================+======================================


	! <<  Does not support floating-point numbers.


==========================================================================;


  internal simple boolean procedure Is.Numeric (string Str);
  begin "Is.Numeric"
    integer Brk;


    return(length(Str) and
	length(Str) = length(scan(Str, Num.Tbl, Brk)));
  end "Is.Numeric";


! ==================================+======================================
==========================================================================;
!! title Rtn Deposit.Str ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                            Rtn Deposit.Str                              =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  internal simple procedure Deposit.Str (string Str; integer Bpt, Cnt(0));
  begin "Deposit.Str"
    if (not Cnt) then
      Cnt _ length(Str);

    while (Cnt) do
    begin
      idpb(lop(Str), Bpt);
      dcr(Cnt);
    end;
  end "Deposit.Str";


! ==================================+======================================
==========================================================================;
!! title Rtn Clean ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                               Rtn Clean                                 =
=                                                                         =
=                                                                         =
====================================+======================================


	Returns a copy of the argument string with leading and trailing
	control characters removed.


==========================================================================;


  internal simple string procedure Clean (string Str);
  begin "Clean"
    own integer
      I,
      J;


    for I _ 1 step 1 until length(Str) do
      if (Str[I for 1] > sp) then
	done;

    for J _ length(Str) step -1 until 1 do
      if (Str[J for 1] > sp) then
	return(Str[I to J]);

    return(null);
  end "Clean";


! ==================================+======================================
==========================================================================;
!! title Rtn Ask.User ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			       Rtn Ask.User				  =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  internal simple boolean procedure Ask.User (string Str);
  begin "Ask.User"
    string Tmp;


    while (true) do
    begin
      if (SubStr(Tmp _ Prompt.User(Str), "Yes")) then
	return(true)
      else if (SubStr(Tmp, "No")) then
	return(false);
    end;
  end "Ask.User";


! ==================================+======================================
==========================================================================;
!! title Rtn Form.Str ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			       Rtn Form.Str				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  internal simple string procedure Form.Str (integer X, Width, Digits(0));
  begin "Form.Str"
    string Tmp;
    integer W;
    integer D;


    getformat(W, D);
    setformat(Width, Digits);
    Tmp _ cvs(X);
    setformat(W, D);
    return(Tmp);
  end "Form.Str";


! ===================================+=====================================
==========================================================================;
!! title Rtn GetTab ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn GetTab				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  internal simple integer procedure GetTab (integer Index, Table);
  begin "GetTab"
    own integer
      Val;


    start!code
      move	1, Table;
      hrl	1, Index;
      setom	!skip!;
      calli	1, '41;
       setzm	!skip!;
      movem	1, Val;
    end;

    return(Val);
  end "GetTab";


! ===================================+=====================================
==========================================================================;
!! title Rtn Parse.File ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			      Rtn Parse.File				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Parse.File("filespec",@Name,@Ext,@PPN)

	Scans the filespec and breaks it into Name, Ext and PPN.
	If a username is given, it is converted to a PPN.


==========================================================================;


  internal procedure Parse.File (string File; reference integer Name, Ext, PPN);
  begin "Parse.File"
    Name _ cvfil(File, Ext, PPN);

    if (PPN = 0) then
      PPN _ calli(0,'24);		! GETPPN;

    if (!lh(PPN) = 0) then
    begin "UserName"
      integer
	Chan,
	Eof;
      integer array
	LkB[0:3];			! block for extended lookup;


      LkB[0] _ arrinfo(LkB, 2);		! size of LkB;
      LkB[1] _ calli('16, '41);		! MFD PPN;
      LkB[2] _ PPN;			! addr of SIXBIT user name;
      LkB[3] _ cvsix("UFD");		! extension;

      open(Chan_getchan, "DSK", '17, 0, 0, 0, 0, Eof);

      if (Eof) then
	return;				! very, very ill;

      chnior(Chan, LkB[0], 4);		! lookup UFD to get PPN;
      release(Chan);			! don't need the channel;
      PPN _ LkB[2];			! save real PPN for user;
    end "UserName";
  end "Parse.File";


! ===================================+=====================================
==========================================================================;
!! title Rtn Run ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				  Rtn Run				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Run(program,[start])

	Run another program (overlay).  Program name may contain
	user name and program name, no device.


==========================================================================;


  internal procedure Run (string File; integer Start(0));
  begin "Run"
    integer array
      RnB[0:5];

    Parse.File(File, RnB[1], RnB[2], RnB[4]);
    RnB[0] _ cvsix("DSK");
    RnB[3] _ RnB[5] _ 0;
    calli(hl(Start) + location(RnB[0]), '35);
  end "Run";


! ===================================+=====================================
==========================================================================;
!! title Rtn Logged.In.User.Name ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			  Rtn Logged.In.User.Name			  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  internal simple string procedure Logged.In.User.Name;
  begin "Logged.In.User.Name"
    return(cv6str(GetTab(-1, -'22)) & cv6str(GetTab(-1, -'21)));
  end "Logged.In.User.Name";


! ===================================+=====================================
==========================================================================;
!! title Rtn Gfd.User.Name ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			     Rtn Gfd.User.Name				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  internal simple string procedure Gfd.User.Name;
  begin "Gfd.User.Name"
    own integer array
      LkB [0:30];
    own integer
      Chan;


    LkB[0] _ 30;
    LkB[1] _ xwd(1, 1);
    LkB[3] _ cvsix("UFD");
    LkB[2] _ GetTab(-1, 2);

    open(Chan _ getchan, "DSK", $F.ASCII, 0, 0, 0, 0, 0);
    chnior(Chan, LkB[0], 4);

    release(Chan);
    return(cv6str(LkB['27]) & cv6str(LkB['30]));
  end "Gfd.User.Name";


! ===================================+=====================================
==========================================================================;
!! title Rtn Ctr.Str ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn Ctr.Str				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Return a string centered on a line of specified length.


==========================================================================;


  internal simple string procedure Ctr.Str (string Str; integer Line.Len);
  begin "Ctr.Str"
    return(Blanks((Line.Len - length(Str _ Clean(Str))) % 2) & Str);
  end "Ctr.Str";


! ===================================+=====================================
==========================================================================;
!! title Rtn Rpt.Str ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn Rpt.Str				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  internal simple string procedure Rpt.Str (string Str; integer Count);
  begin "Rpt.Str"
    string
      S;

    S _ null;

    while (Count) do
    begin
      appnd(S, Str);
      dcr(Count);
    end;

    return(S);
  end "Rpt.Str";


! ===================================+=====================================
==========================================================================;
!! title Rtn Ceiling ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn Ceiling				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Return the least integer >= (X/Y).


==========================================================================;


  internal simple integer procedure Ceiling (integer X, Y);
  begin "Ceiling"
    return((X + Y - 1) div Y);
  end "Ceiling";


! ===================================+=====================================
==========================================================================;
!! title Rtn RescanCommandLine ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=                             Rtn RescanCommandLine                       =
=                                                                         =
=                                                                         =
=====================================+=====================================


        Rescan the command line.  Return the line, or null if none
        available.  (This can only be done once successfully.)


==========================================================================;


  internal simple string procedure RescanCommandLine;
  begin "RescanCommandLine"
    integer
      Flag;

    start!code
      setob     1, Flag;
      ttcall    6, 1;                   ! getlch 1 ;
      tlnn      1, '100000;             ! lc.ecs - command lines exists ;
        setzm   Flag;
    end;

    if (not Flag) then
      return(null);

    start!code
      ttcall      '10,;		        ! rescan ;
    end;

    return(inchwl);
  end "RescanCommandLine";


! ===================================+=====================================
==========================================================================;

end "Util";

! **********************************+**************************************
**************************************************************************;
    &-[)÷