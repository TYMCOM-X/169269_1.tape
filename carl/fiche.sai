Begin

require ('15 lsh 18) lor '0 version;

require "MYSAIL.DEF" source!file;
require "CMDSCN.REQ" source!file;

Define TENEX = False;
Ifcr Tenex
 thenc
    define library!directory = {};
 elsec
    define library!directory = {"(SAILIB)"&};
 endc

    require library!directory "BLOCK.REQ"  source!file;
    require library!directory "TXTSUB.REQ" source!file;
    require library!directory "MATCHS.REQ" source!file;

Ifcr not TENEX
 thenc
    define Cfile(x) = { Close( x ); Release( x ) };
 endc

define	Spaces = {"                                    "} &
		 {"                                    "};

define  Center(s,l) = { (spaces[1 for (l-length(s)) div 2] &
			 s & spaces)[1 for l] };

define	!Block(Group, Name, Size) = {
	Ifcr Length(Cvps(Size)) = 0 thenc assignc Size = 1; endc
	Define Group}&{Name = Group;
	Redefine Group}&{Max = Group;
	Redefine Group = Group + Size;};

define	M. = 0;		    define X42. = 0;
    !Block( M.,None )		!Block( X42., Auto )
    !Block( M.,Setup )		!Block( X42., 36F )
    !Block( M.,Bliss )		!Block( X42., 24F )
    !Block( M.,TymMac )		!Block( X42., 12F )
    !Block( M.,DecMac )		!Block( X42., 12H )

preset!with "NONE","SETUP","BLISS-10","TYMMACRO-46","DECMACRO-53";
string array modes[M.None:M.DecMac];
preset!with "AUTO", "36F", "24F", "12F", "12H";
string array Xformats[ X42.Auto : X42.Max ];

preset!with 0,1, 1, 1, 1;	integer array PageLine[0:M.Max];
preset!with 0,0, 0, 0, 0;	integer array PageBeg[0:M.Max];
preset!with 0,0, 0, 0, 0;	integer array PageEnd[0:M.Max];
preset!with 0,2, 3, 2, 5;	integer array TypeLine[0:M.Max];
preset!with 0,0, 0, 0, 0;	integer array TypeBeg[0:M.Max];
preset!with 0,0, 0, 0, 0;	integer array TypeEnd[0:M.Max];
preset!with 0,3, 3, 4, 4;	integer array LineLine[0:M.Max];
preset!with 0,1, 9, 1, 1;	integer array LineBeg[0:M.Max];
preset!with 0,8,12, 6, 6;	integer array LineEnd[0:M.Max];

preset!with
    "January", "February", "March",     "April",   "May",      "June",
    "July",    "August",   "September", "October", "November", "December";
own safe string array  Months [0:11];	!  Note: "Months[i][1 for 3]" ;

string array TitleLines[ 1:2 ];
string HighDate, LowDate;

define MaxType = 10;
string array TypeString[1:MaxType], TypeType[1:MaxType];

string FileSpec, FileName, SymFile;
string TextLine, FileList, FileTitle, Titles, Title, GTitle, PTitle;
string StrPage, StrLine, StrType, StrTest, StrLoc;

integer Brk, RealBrk, LastBrk, InBrk, InSpc, InLst, InNam;
integer Fiche, SPage, PPage, Pages, Lines, Mode, Format;
integer Cipher, S.Width, S.Height, S.TypeLength, Margin, Edge;
integer S.PageLine, S.PageBeg, S.PageEnd;
integer S.TypeLine, S.TypeBeg, S.TypeEnd;
integer S.LineLine, S.LineBeg, S.LineEnd;
integer FicheChan, DiskChan, SymChan;
integer RealEof, Eof, SymEof, Oeuf, Stop, Totals, HeaderTop, AskMe;
integer NumberAll, I, Count, Ficherow, Fichecol;

Simple String Procedure CvDate ( integer Date.In );
begin "CvDate"
    own string Day, Month, Year;
    integer Digits, Width;

    getformat(Width, Digits);
    setformat(-2, 0);

    Day _ cvs( (Date.In mod 31) + 1 );
    Month _ Months[ (Date.In div 31) mod 12 ][1 for 3];
    Year _ cvs( (Date.In div (31 * 12)) + 1964 );

    setformat(Width, Digits);

    return(  Day & "-" & Month & "-" & Year );

end "CvDate";


simple procedure initialize;
begin "init"

setbreak(inbrk_getbreak,#LF&#HT&#FF,#CR,"INS");	! break on LF FF HT;
setbreak(inlst_getbreak,",",Null,"KINS");	! CAP & break on comma;
setbreak(inspc_getbreak," "&#HT,Null,"XNR");	! Whitespace;
setbreak(innam_getbreak,"().<>",Null,"KINS");	! userstuffs;

LowDate_ CvDate( Calli( 0, '14 ) );		! get today's date;
HighDate_ LowDate[1 for 6];			! setup for proper;
LowDate_ " " & LowDate[8 for 4]  & "       ";	! data fields.;

end "init";
require initialize initialization;

Simple Procedure OpenFicheFiles;
begin "open files"

    If Mode > 0
     then begin "symloc file"

      If ( symchan )			! close any open symloc file;
       then CFile( symchan );

      Open(symchan_getchan, "DSK",0, 0,4, 0,0, symeof_-1);
      Enter(symchan,Symfile_"FSH"&CVS(Fiche+1000)[2 for 3]&".MFS",symeof_-1);

     end "symloc file";

    If ( FicheChan )			! close any open fiche file;
     then CFile( FicheChan );

    Open(FicheChan_getchan, "DSK",0, 0,4, 0,0,oeuf_-1);
    Enter(FicheChan, filename_"FSH"&CVS(Fiche+1000)[2 for 3]&".FSH", oeuf_0);

    Print("Writing file ", filename,
	If Mode > 0 then " ...symloc info in file "&Symfile else "", crlf);

    FicheRow_ FicheCol_ Pages_ 0;	! initialize fiche parameters;
    StrLoc_ "B  1";			! row 0 - column 0 ==> B 1;

end "open files";


Simple Procedure BumpFicheFrame;
begin

    Pages_ Pages + 1;			! increment page count;
    FicheRow_ FicheRow + 1;		! increment row 0-12;

    If FicheRow > 12			! if > 12 then reset;
     then begin "next column"

	FicheCol_ FicheCol + 1;		! column up by 1, 0-15;
	FicheRow_ 0;			! row back to 0;

     end "next column";

    If FicheCol > 15			! if > 15 then reset;
     then begin "new fiche"

	Print(" to ",Pages-1," (",(pages-1)-spage,")",Crlf);

	Fiche_ Fiche + 1;
	OpenFicheFiles;
	SPage_ Pages_ 1;
	Print("   ",Filespec," pages ",Pages);

	Titles_ Titles & ("   "&cvs(Fiche))[inf-3 to inf] &
		" " & PTitle[10 for 2] & " " &
		PTitle[13 to INF-24] & HighDate & LowDate & Crlf;

	PTitle_  PTitle[1 to INF-24]	! re-do the fiche numbering;
	  & HighDate
	  & (If ( NumberAll )
	      then ("      "&cvs(Fiche))[inf-5 for 6]
	      else ("      ") )
	  & LowDate;

	CPrint( FicheChan, #FF & PTitle, Crlf );

     end "new fiche"
     else begin "old fiche"

	StrLoc_ ("B"+FicheRow) &	! convert to location;
	     ("   " & Cvs(FicheCol+1))[inf-3 to inf];

	Cprint(FicheChan, #FF);		! trip the actual fiche page;

     end "old fiche";

end;

simple integer procedure SP( String P; Integer Def );
Return( If Mode = M.Setup and		! For setup mode;
	   0 < I_ Cvd( CReadL(P) )	!  then ask the question;
	 then I				!   and do the right thing;
	 else Def );			!  else default answer;

simple integer procedure SPO( String P; Integer Def );
Return( If Mode = M.Setup and		! For setup mode;
	   0 < I_ Cvo( CReadL(P) )	!  then ask the question;
	 then I				!   and do the right thing;
	 else Def );			!  else default answer;

simple boolean procedure SPy( String P; Boolean Def );
begin
    String S;
    Return( If length( S_ CReadL( P & " (Y or N)? " ))
	     then Kequ( S, "YES"[1 for length(S)] )
	     else Def );
end;


simple string procedure TypeMatch;
begin
    If S.TypeLength > 0
     then For I_ 1 step 1 until S.TypeLength
	   do If StrMatch( TypeString[I], TextLine ) > 0
	       then Return( TypeType[I] );
    Return( StrType );
end;

simple string procedure FindType( String Str );
Return( Case Mode of (
	[M.TymMac] [M.DecMac]				! Macro;
	    (If StrMatch( "SYMBOL TABLE", Str ) then "S"
	     else If StrMatch( "SYMBOL CROSS REFERENCE", Str ) then "C"
	     else If StrMatch( "MACRO/OPDEF CROSS REFERENCE", Str ) then "M"
	     else If StrMatch( "OPCODE CROSS REFERENCE", Str ) then "O"
	     else StrType ),
	[M.Setup]
	    (If S.TypeBeg > 0
	      then Str[S.TypeBeg to S.TypeEnd]
	      else TypeMatch),
	[M.Bliss]
	    (If StrMatch( "Identifier BL  Decl TYP USED", Str ) then "C"
	     else StrType ),
	[else] StrType ) );

simple string procedure FindPage( String Str );
begin
    Integer F; String S;
    Return( Cvs(If Mode > M.None			! ? Doing anything;
		 then Cvd(If S.PageBeg > 0		! Setup mode;
		     then Str[S.PageBeg to S.PageEnd]	! Given columns;
		     else If F_ StrMatch("Page",Str)	!  ? none;
			   then Str[F to Inf]		!  search string;
			   else StrPage  )		!  or return absolute;
		 else Pages - SPage  ) );		! else absolute page;
end;

simple string procedure FindLine( String Str );
begin
    Integer F; String S;
    F_ Cvd( S_CToken(Str) );
    Return( If Mode > M.None
	     then If Kequ(S,Cvs(F)) then S else StrLine
	     else Cvs(Totals) );
end;

simple procedure SymlokChecks;
begin
    If Lines = S.PageLine - 1
     then StrPage_ FindPage( TextLine );
    If Lines = S.TypeLine - 1
     then StrType_ FindType( TextLine );
    If Lines geq S.LineLine - 1
     then StrLine_ FindLine( If S.LineBeg > 0
			      then TextLine[S.LineBeg to S.LineEnd]
			      else TextLine );
end;


procedure AdvanceFrame;
! ----------------------------------------------------------------------;
!									;
!	AdvanceFrame	Bump page counts and fiche frame counters.	;
!									;
! ----------------------------------------------------------------------;
begin

    If Mode > M.None and StrType = null	! if doing symloc;
     then Cprint(symchan,		!  stuff, then print;
	   FileTitle,",",		!  it all out: title;
	   StrLine,",",StrLoc,",",	!  source-line, fiche-loc;
	   StrPage, crlf);		!  and source-page;

    BumpFicheFrame;			! increment frame #;
    Lines_ 0;				! set line count;
    ppage_ ppage + 1;			!  ...and printing page;

    Print( If ppage land 1
	    then " " else #BS );	! indicate we're think

end;

simple procedure Print!Heading;
! ----------------------------------------------------------------------;
!									;
!	Print!Heading	Print a page header on the microfiche.		;
!									;
! ----------------------------------------------------------------------;
begin
    string dir,ext;

    scan(filespec,inspc,brk);			! eat white space;
    TextLine_ Filespec;				! copy so we can eat text;

    while ( length(TextLine) )
     do begin "scan text"

	filename_ scan(TextLine,innam,brk);	! read part of a name;

	if brk = ")" or brk = ">"
	 then dir_ filename;			! if ")>" then was username;

	if brk = "."
	 then ext_ TextLine;			! if "." then set extension;

	If length(filename) and (brk = "." or brk = 0)
	 then TextLine_null;			! if not eol or ext loop;

     end "scan text";

    FileTitle_ Filename[1 to 6];		! set title to first 6;

    Cprint(FicheChan, Crlf);			! print header page;
    Block(Filename[1 for 3], FicheChan, 6, 3, Cipher, 3, edge, 3,0);
    Block(Filename[4 for 3], FicheChan, 6, 3, Cipher, 3, edge, 3,3);
    Block((if length(dir) then "["&dir&"]" else filename[1 for 14])&
	  (if length(ext) then " ."&ext[1 for 3] else ""),
			     FicheChan, 1, 1, Cipher, 3, edge, 1,1);

    StrType_ StrLine_ null;			! clear symloc labels;
    StrPage_ "0";				! let header be so marked;
    Totals_ ppage _ Lines_ 0;			! clear file counts;

    AdvanceFrame;				! go to next frame;
    Print("  ",Filespec," pages ",Pages);	! print message;
    ppage_ 0;					! set pages to 0;
    LastBrk_ #FF;				! avoid double FF;

end;

boolean procedure Infile( String F; Boolean Flag( True ) );
! ----------------------------------------------------------------------;
!									;
!	InFile		Open file, printing error messages, if any.	;
!			Returns "TRUE" if no errors.			;
!									;
! ----------------------------------------------------------------------;
begin
    open(diskChan_getchan, "DSK",0, 4,0, 512, RealBrk, RealEof_-1);
    lookup(diskChan, Filespec, RealEof_-1);
    if ( RealEof and Flag )
     then Print( "File: ", Filespec, " not found."& Crlf );
    return( RealEof = 0 );
end;


string procedure get!line;
! ----------------------------------------------------------------------;
!									;
!	Get!Line	Read a line from the input file.		;
!									;
! ----------------------------------------------------------------------;
begin
    Own integer MoreText;
    Own string  OurLine;

    If not MoreText				! if buffer empty;
     then begin					!  then get more;
	OurLine_ input( diskChan, inbrk );	!   read part of a line;
	While not RealEof and			!   while not at eof;
	  RealBrk neq #LF and RealBrk neq #FF	!   and not a line-break;
	 do OurLine_ OurLine& (If RealBrk = #HT	!     for tabs, expand them;
		  then Spaces[1 to 8 - (Length(OurLine) mod 8)]
		  else RealBrk) &		!     keep everything else;
		 input( diskChan, inbrk );	!   add to line + more input;
     end
     else OurLine_ OurLine[S.Width+1 to INF];	!  else clip buffer;
    MoreText_ Length( OurLine ) > S.Width;	! if too many characters;
    Brk_ If MoreText then #LF else RealBrk;	! set break and;
    Eof_ RealEof and (not MoreText);		!  eof flags appropriately;
    Return( OurLine[1 to S.Width]		! Return a line;
	    & (If Length(OurLine) or		!  ...if non-blank;
		  RealBrk neq #FF		!      or not a FF;
		then Crlf else "") );		!  ...add a CRLF;
end;


Procedure Each!file;
! ----------------------------------------------------------------------;
!									;
!	Each!File	Handle the formatting of one file.		;
!			Assumes all necessary channels for output	;
!			have been openned.				;
!									;
! ----------------------------------------------------------------------;
Begin "Each File"

    If InFile( Filespec, False )		! if we have a file;
     then begin "Count pages"			! then format it;

	SPage_ Pages;				! remember start page;
	Print!heading;				! do header stuff;
	Eof_ False;				! not EOF, so set flag;

	While not( eof )
	 do begin "Reading between the lines"

	    TextLine_ get!line;			! read line or partial line;

	    If not eof or length(TextLine)	! ? eof or null line;
	     then begin "Counting breaks"

		Cprint(FicheChan,TextLine);	! print a line of data;

		If Mode > M.None		! check for higher modes;
		 then SymlokChecks;		! of life, such as symlok;

		If brk = #LF			! for normal "or" overflow;
		 then Lines_ Lines + 1;		!  then increment lines;

		If brk = #FF or			! if form-feed;
		   Lines geq S.Height		!  or page overflow;
		 then begin "do FF"		!  then do page-advance;
		    Brk_ #FF;			! change break char;
		    If LastBrk neq #FF or	! if FF not after FF;
		       length(TextLine)		!  or text on line;
		     then AdvanceFrame;		!  then advance the frame;
		 end "do FF";

		LastBrk_ brk			! copy breaks;

	     end "Counting breaks"
	 end "Reading between the lines";

	If brk neq #FF then AdvanceFrame;
	Print(" to ",Pages," (",pages-spage,")",Crlf);

     end "Count pages";

    Cfile( diskChan );

 end "Each File";

Simple Procedure GetSetupMode;
begin "Setup Mode"
    String Str;

    Do begin "get proper mode"
	Str_ CReadL("Mode (type ? for help): ");
	If Length( Str ) = 0			! <CRLF> is;
	 then Mode_ M.None			!   the same as "NONE";
	 else Mode_ ArrMatch( Str, Modes ) - 1;	!   else match entries;
	If Mode < M.None			! ? Unknown mode;
	 then Print( Crlf,"Supported modes are:",
		 Crlf,"  NONE         normal text file",
		 Crlf,"  SETUP        special setup dialogue",
		 Crlf,"  BLISS-10     listing from BLISS-10/20",
		 Crlf,"  TYMMACRO-46  listing from Tymshare MACRO",
		 Crlf,"  DECMACRO-53  listing from MACRO 53",
		 Crlf,"  a carriage-return defaults to NONE", Crlf,
		 Crlf );
     end "get proper mode" until Mode geq M.None;

    S.Height_   SP( " Page height: ", 60 );
    S.Width_    SP( " Page width: ", 132 );
    Edge_       SP( " Right edge: ", S.Width );
    Cipher_     SPo( " Cipher value: ", -'177 );

    If Mode = M.Setup and not SPy( " Symlok output", False )
     then Mode_ M.None;

    S.PageLine_ SP( " 'Page n' line: ", PageLine[Mode] );
    S.PageBeg_  SP( " 'Page n' start column: ", PageBeg[Mode] );
    S.PageEnd_  If S.PageBeg > 0
		 then SP( " 'Page n' end column: ", PageEnd[Mode] )
		 else 0;

    S.TypeLine_ SP( " Type line: ", TypeLine[Mode] );
    S.TypeBeg_  SP( " Type start column: ", TypeBeg[Mode] );
    If S.TypeBeg > 0
     then S.TypeEnd_ SP( " Type end column: ", TypeEnd[Mode] )
     else If Mode = M.Setup then begin
	S.TypeLength_ 0;
	For I_ 1 step 1 until MaxType
	 do begin
	    TypeString[I]_ CReadL( " Match #"&Cvs(I)&": ");
	    If Length( TypeString[I] ) = 0
	     then Done;
	    TypeType[I]_ CReadL( " Type #"&Cvs(I)&": ");
	    S.TypeLength_ I;
	 end;
	If I geq MaxType
	 then Print( Crlf, "** Search string limit reached **",
		     Crlf );
     end;

    S.LineLine_ SP( " Numbered lines start at line: ", LineLine[Mode] );
    S.LineBeg_  SP( " Number start column: ", LineBeg[Mode] );
    S.LineEnd_ If S.LineBeg > 0
		then SP( " Number end column: ", LineEnd[Mode] )
		else 0;
end "Setup Mode";

Simple Integer Procedure TitleFormat;
begin "Setup Format"
    String  Str;
    Integer Fmt;

    Do begin "get proper format"

	Str_ CReadL("Title Format (type ? for help): ");

	If ( Length( Str ) )
	 then Fmt_ ArrMatch( Str, XFormats ) - 1
	 else Fmt_ X42.Auto;

	If ( Fmt < X42.Auto )
	 then Print( Crlf & "Supported title formats are:" &
		     Crlf & "  Auto    Pick best fit (or ask)" &
		     Crlf & "  36F     Full height 36" &
		     Crlf & "  24F     Full height 24, Half 12/12" &
		     Crlf & "  12F     Full height 12, Half 24/24" & Crlf &
		     Crlf & "  a carriage-return defaults to ""Auto""" &
		     Crlf & "  date & fiche number appear at right." &
		     Crlf & Crlf );

     end "get proper format" until Fmt geq X42.Auto;

    Return( Fmt );

end "Setup Format";


Simple string procedure GetSome( Reference String S; Integer G, L );
begin
    String Str;

    Str_ null;

    While ( length( S ) geq L ) and ( length( Str ) < G )
     do Str_ Str & (if length(Str) then "," else "") &
		scan( S, inlst, brk );

    If ( length( Str ) > G ) or ( length( S ) > L )
     then begin "take drastic action"

	S_ Str & "," & S;
	Str_ S[1 for G];
	S_ S[G+1 for L];

     end "take drastic action";

    Return( Str );

end;

Simple string procedure MakeTitle( String S );
begin
    Integer UseFmt;				! current format;
    String  Dir, Ext;				! temps;
    String  FTitle, CTitle;			! Fiche, center;

    CTitle_ null;
    While Length( Filespec_ Scan(S, inlst, brk) )
     do begin "each specification"		! one file at a time;

      If InFile( Filespec )			! if we have a file;
       then begin "file found"

	while ( length(Filespec) )
	 do begin "scan text"

	    filename_ scan(Filespec,innam,brk);	! read part of a name;

	    if brk = "."
	     then ext_ Filespec;		! if "." then set extension;

	    If length(filename) and (brk = "." or brk = 0)
	     then Filespec_null;		! if not eol or ext loop;

	 end "scan text";

	CTitle_ CTitle & ( If ( length( CTitle ) )
			    then "," else "") & Filename[1 to 6];

       end "file found";
  
    Cfile( diskChan );
  
   end "each specification";

    If ( length( GTitle ) )			! if global title is known;
     then FTitle_ GTitle			!  then use it here;
     else FTitle_ CReadL( "Fiche title: " );	!  else prompt for one.;

    If ( AskMe )
     then CTitle_ CReadL( "File titles: " )	! get the file title;
     else If not( length( FTitle ) )
	   then FTitle_ Scan( FTitle_ CTitle, inlst, brk );

    If ( Format = X42.Auto )
     then begin "determine use format"

	UseFmt_ (length(FTitle) + 11) div 12;

	If ( 0 < UseFmt < 4 )
	 then UseFmt_ 4 - UseFmt
	 else begin "truncate title"
	    Print( "Main fiche title too long;" &
	       " truncating to 36 characters." & Crlf );
	    UseFmt_ 1;
	    FTitle_ FTitle[1 for 36];
	 end "truncate title";

	If ( length(CTitle) )
	 then begin "check out center"

	    Case UseFmt of begin "fixup center"
	    [X42.36F] ! [1] ;
	      begin
		  Print( "% file titles ignored." );
		  CTitle_ null;
	      end;
	    [X42.24F] ! [2] ;
	      If length( CTitle ) > 24
	       then begin
		  Print( "% file title truncated." );
		  CTitle_ CTitle[1 for 24];
	       end
	       else if length( CTitle ) leq 12
		     then begin
			FTitle_ (FTitle&spaces)[1 for 24] &
				Center( CTitle, 12 );
			CTitle_ null;
			UseFmt_ X42.36F;
		     end;
	    [X42.12F] ! [3] ;
	      If length( CTitle ) > 48
	       then begin
		  Print( "% file title truncated." );
		  CTitle_ CTitle[1 for 48];
	       end
	       else if length( CTitle ) leq 24
		     then begin
			FTitle_ (FTitle&spaces)[1 for 12] &
				Center( CTitle, 24 );
			CTitle_ null;
			UseFmt_ X42.36F;
		     end
	     end "fixup center";
	    
	 end "check out center";

     end "determine use format"
     else UseFmt_ Format;

    Case UseFmt of begin "build titles"

    [X42.36F] ! [1] ;
      FTitle_ ( FTitle & Spaces )[1 for 36];
    [X42.24F] ! [2] ;
      begin
	FTitle_ ( FTitle & Spaces )[1 for 24];
	Dir_ GetSome( CTitle, 12, 12 );
	CTitle_ Center( Dir, 12 ) & Center( CTitle, 12 );
      end;
    [X42.12F] ! [3] ;
      begin
	FTitle_ ( FTitle & Spaces )[1 for 12];
	Dir_ GetSome( CTitle, 24, 24 );
	CTitle_ Center( Dir, 24 ) & Center( CTitle, 24 );
      end

    end "build titles";

    Titles_ Titles & ("   "&cvs(Fiche))[inf-3 to inf] &
	    " " & cvs( UseFmt+100 )[2 for 2] & " " &
	    FTitle & CTitle & HighDate & LowDate & Crlf;

    Return( "42XFORMAT" & cvs( UseFmt+100 )[2 for 2] & ":" &
	    FTitle & CTitle & HighDate &
	    (If ( NumberAll )
	      then ("      "&cvs(Fiche))[inf-5 for 6]
	      else ("      ") ) &
	    LowDate
	  );

end;



Print("[FICHE version ",			! Banner of Identifcation;
      Cvos(!lh(memory['137])), ".", Cvos(!rh(memory['137])), "]", Crlf);

Mode_   M.Setup;				! indicate setup mode;
CmdTyp_ True;					! force type-out;
Format_ TitleFormat;				! prompt for format;
GTitle_ CReadL( "Global title: " );		! ask for group title;
Fiche_  SP( "Initial Fiche No.: ", 1 );		! initialize fiche number;
NumberAll_ SPy( "Number fiche in top right", False );
HeaderTop_ SPy( "Headers in TOP row", False );	! put headers in frame ?2;
AskMe_  SPy( "Override file titles", False );	! prompt for file titles;

GetSetupMode;					! initialize the world;

While Length( FileList_ CReadL(Crlf&"Fiche #"&Cvs(Fiche)&": ") )
 do begin "each fiche"				! setup output file;

    OpenFicheFiles;
    CPrint( FicheChan, #FF & (PTitle_ MakeTitle( FileList )), Crlf );

    While Length(FileList)
     do begin "processing fiche"

	Filespec_ Scan(FileList,inlst,brk);	! gobble one file-spec;
	If Length(Filespec) then Each!File;	! one file at a time;
	If ( HeaderTop )			! if want headers at top;
	 then while ( Pages mod 13 )		!  then while not at top;
		do BumpFicheFrame;		!  do the necessary things;

     end "processing fiche";

    Cfile( FicheChan );			! close fiche data;
    If Mode > 0 then Cfile( symchan );	! close symloc data;

    Fiche_ Fiche + 1;			! count fiche;

end "each fiche";

Print( Crlf, Crlf, "Fiche titles", Crlf, Crlf, Titles, Crlf, Crlf );

Call(0,"EXIT");  ! exit to monitor without "end of sail...";

end;
  )&*µ