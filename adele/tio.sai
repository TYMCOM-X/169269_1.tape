begin "TIO version 6"

require "(sailib)sail.def" source!file;

!  A rewrite of the older SIMPL program (circa '74) to get this mess
!  into a supported language.  Also to make some additions and fixes
!  that nobody dared do in SIMPL.
!
!  Revision History:
!
!  20-Sep-74	Version 1. First release.  Written by Gary Gere.
!   5-Mar-75	Version 2. Major mods. - Gary Gere.
!	(various edits lost to history)
!  Jul/Aug,Oct-81  Version 6. Complete rewrite into SAIL. - Dave W. Smith.
!  Oct-82  Oh well...  /DWS
!  Edits to version 6:
!
;
define Version!Number = 6, Edit!Number = 0;
require Version!Number lsh 18 + Edit!Number version;
! The primary command table is preloaded here ;

! *** If you change the order of any of the below, be sure to change
! *** the case statement dispatch in the main command loop ;

  preload!with
    "?", "HELP",
    "DELAY", "A", "B", "C",
    "CRLF", "EXPLAIN",
    "FDX", "FULLDUPLEX", "FILLER", "FFILL", "HDX", "HALFDUPLEX",
    "ID", "TABFILL", "TERMINAL", "WIDTH", "WHAT", "QUIT";
  string array Primary!Options[ 1 : 22 ];
!  The command scanning/parsing routines ;

  string Cmd;			! The current command line ;
  integer BrkTab, BrkChar;	! Break table and break character ;


!  Initialize the token scanner ;

procedure SetupTokenScanner;
  begin
    Cmd _ NULL;
    BrkTab _ getbreak;
    breakset( BrkTab, NULL, "K" );		! Konvert all to upper case ;
    breakset( BrkTab, ('40&'11&NULL), "IS" );	! skip leading spaces, tabs ;
    breakset( BrkTab, (", "&'11&NULL), "IA" );	! break on punctuation ;
    breakset( BrkTab, (", "&'11&NULL), "O" );	! omit these ;
  end;


!  The token scanner.  Scan the next token from the current command line.
!  If the command line is null and 'PromptFlag' is true, then prompt with
!  'Prompt' and read a new command line, else return NULL;

string procedure TokenScan( string Prompt; boolean PromptFlag );
  begin
    if ( equ( Cmd, NULL )) then
      if ( not PromptFlag ) then
	return( NULL )
      else 
	begin
	  print( Prompt ); Cmd _ inchwl;
	end;
    return( scan( Cmd, BrkTab, BrkChar ));
  end;


!  Predicate to tell us if there's more to scan ;

boolean procedure MoreToScan;
  begin
    return( Cmd neq NULL );
  end;


! Flush out the current command ;

procedure FlushCommand;
  begin
    Cmd _ NULL;
  end;
!  Routine to look for a matching string in a string table.  Case IS
!  important.  If 'AbrFlag' is true then a valid abbreviation will be
!  accepted.  The routine returns the index of the string in the table
!  if found, zero if not found, or -1 if abbreviations are allowed and
!  the string is ambiguous. ;

integer procedure Match( string S; string array S!Table; boolean AbrFlag );
  begin
    integer I, Len!S, Len!S!Table, match;

    Len!S _ length( S );
    Len!S!Table _ arrinfo( S!Table, 2 );

    match _ 0;				! no match yet ;
    for I := 1 upto Len!S!Table do
      begin
        if ( equ( S, S!Table[ I ] )) then
          return( I );			! a match! ;
        if ( AbrFlag and equ( S, S!Table[ I ][ 1 for Len!S ] )) then
          if ( match neq 0 ) then
            return( -1 )		! oops, ambiguous abbreviation ;
          else
            match _ I;			! possible match ;
      end;
    return( match );			! return 0 or valid abbreviation ;

  end;
!  Routine to format and print a string table, with as many entries on
!  a line as space allows, seperated by commas, and each line indented
!  four spaces. ;

procedure PrettyPrint( string array S!Table );
  begin
    integer I, S!Table!Len;
    string L;

    S!Table!Len _ arrinfo( S!Table, 2 );
    L _ "    ";
    for I _ 1 upto S!Table!Len do
      begin
	if ( (length( L ) + length( S!Table[ I ] )) + 3 ) > 72 then
	  begin
	    print( L, crlf );
	    L _ "    " & S!Table[ I ];
	  end
	else
	  L _ L & S!Table[ I ];
	if ( I < S!Table!Len ) then
	  L _ L & ",  ";
      end;
    if ( length( L ) > 4 ) then
      print( L, crlf );
  end;
! Handy option error and listing commands ;

procedure ListOptions( string Command; string array Options );
  begin
    print( "Valid ", Command, " options are:", crlf );
    PrettyPrint( Options );
    print( crlf );
  end;


procedure OptionError( string Command, Option );
  begin
    FlushCommand;
    print( "? '", Option, "' is not a valid ", Command, " option.", crlf );
    print( "% Type '", Command, " ?' for a list of valid options.", crlf );
    print( crlf );
  end;

!  Routines to do the GETLCH and SETLCH monitor calls ;

simple integer procedure GETLCH;
  begin
    start!code
	define getlch = '051300000000;
	setom	1;
	getlch	1;
    end;
  end;


procedure SETLCH( integer value );
  begin
    start!code
	define setlch = '051340000000;
	move	1,value;
	setlch	1;
    end;
  end;
!  Routines to (re)set each of the network terminal types.  This checks
!  out with the code in SCNSER to set the defaults for a given TID. ;

simple procedure Set!Terminal!Type!A( integer In );
  begin
    if ( In ) then
      auxclv( -1, '15,  2 )	! input baud rate ;
    else
      begin
	auxclv( -1, '24,  0 );	! host can send xon ;
	auxclv( -1, '27, 72 );	! width 72 ;
	auxclv( -1, '30,  0 );	! filler class 1 ;
	auxclv( -1, '31,  0 );	! filler class 2 ;
	auxclv( -1, '16,  2 );	! output baud rate ;
	auxclv( -1, '17,  0 );	! no parity ;
	auxclv( -1, '20,  0 );	! full duplex ;
	auxclv( -1, '21,  0 );	! paramater A ;
	auxclv( -1, '22,  0 );	! paramater B ;
	auxclv( -1, '23,  0 );	! paramater C ;
	auxclv( -1, '14,  1 );	! delay computed on CR ;
      end;
  end;

simple procedure Set!Terminal!Type!B( integer In );
  begin
    if ( In ) then
      begin
	auxclv( -1, '15,  1 );	! input baud rate ;
	return;
      end;
    auxclv( -1, '24,  0 );	! host can send xon ;
    auxclv( -1, '27, 72 );	! width ;
    auxclv( -1, '30,  1 );	! fill class 1 ;
    auxclv( -1, '31,  1 );	! fill class 2 ;
    auxclv( -1, '16,  1 );	! output buad rate ;
    auxclv( -1, '17,  0 );	! no parity ;
    auxclv( -1, '20,  0 );	! full duplex ;
    auxclv( -1, '21,  6 );	! paramater A ;
    auxclv( -1, '22,  0 );	! paramater B ;
    auxclv( -1, '23,  2 );	! paramater C ;
    auxclv( -1, '14,  1 );	! compute delay on CR ;
  end;

simple procedure Set!Terminal!Type!C( integer In );
  begin
    if ( In ) then
      begin
	auxclv( -1, '15,  2 );	! input baud rate ;
	return;
      end;
    auxclv( -1, '24,  0 );	! host may send xon ;
    auxclv( -1, '27,120 );	! width ;
    auxclv( -1, '30,  3 );	! filler class 1 ;
    auxclv( -1, '31,  2 );	! filler class 2 ;
    auxclv( -1, '16,  2 );	! output baud rate ;
    auxclv( -1, '17,  0 );	! no parity ;
    auxclv( -1, '20,  0 );	! full duplex ;
    auxclv( -1, '21,  4 );	! paramater A ;
    auxclv( -1, '22,  0 );	! paramater B ;
    auxclv( -1, '23,  4 );	! paramater C ;
    auxclv( -1, '14,  1 );	! compute delay on CR ;
  end;

simple procedure Set!Terminal!Type!D( integer In );
  begin
    if ( In ) then
      begin
	auxclv( -1, '15,  0 );	! input baud rate ;
	return;
      end;
    auxclv( -1, '24,  0 );	! host may send xon ;
    auxclv( -1, '27, 72 );	! width ;
    auxclv( -1, '30,  1 );	! filler class 1 ;
    auxclv( -1, '31,  1 );	! filler class 2 ;
    auxclv( -1, '16,  0 );	! output baud rate ;
    auxclv( -1, '17,  0 );	! no parity ;
    auxclv( -1, '20,  0 );	! full duplex ;
    auxclv( -1, '21,  0 );	! paramater A ;
    auxclv( -1, '22,  0 );	! paramater B ;
    auxclv( -1, '23,  0 );	! paramater C ;
    auxclv( -1, '14,  1 );	! delay computed on CR ;
  end;

simple procedure Set!Terminal!Type!E( integer In );
  begin
    if ( In ) then
      begin
	auxclv( -1, '15,  2 );	! input baud rate ;
	return;
      end;
    auxclv( -1, '14,  1 );	! compute delay on CR ;
    auxclv( -1, '24,  0 );	! host may send xon ;
    auxclv( -1, '27, 72 );	! width ;
    auxclv( -1, '30,  1 );	! filler class 1 ;
    auxclv( -1, '31,  1 );	! filler class 2 ;
    auxclv( -1, '16,  2 );	! output baud rate ;
    auxclv( -1, '17,  0 );	! no parity ;
    auxclv( -1, '20,  0 );	! full duplex ;
    auxclv( -1, '21,  4 );	! paramater A ;
    auxclv( -1, '22,  6 );	! paramater B ;
    auxclv( -1, '23,  6 );	! paramater C ;
    auxclv( -1, '14,  1 );	! compute delay on CR ;
  end;
simple procedure Set!Terminal!Type!F( integer In );
  begin
    if ( In ) then
      begin
	auxclv( -1, '15,  1 );	! input baud rate ;
	return;
      end;
    auxclv( -1, '24,  0 );	! host may send xon ;
    auxclv( -1, '27,120 );	! width ;
    auxclv( -1, '30,  3 );	! filler class 1 ;
    auxclv( -1, '31,  2 );	! filler class 2 ;
    auxclv( -1, '16,  2 );	! output baud rate ;
    auxclv( -1, '17,  0 );	! no parity ;
    auxclv( -1, '20,  0 );	! full duplex ;
    auxclv( -1, '21,  4 );	! paramater A ;
    auxclv( -1, '22,  0 );	! paramater B ;
    auxclv( -1, '23,  4 );	! paramater C ;
    auxclv( -1, '14,  1 );	! compute delay on LF ;
  end;

simple procedure Set!Terminal!Type!G( integer In );
  begin
    if ( In ) then
      begin
	auxclv( -1, '15,  2 );	! input baud rate ;
	return;
      
    auxclv( -1, '24,  0 );	! host may send xon ;
    auxclv( -1, '27,120 );	! width ;
    auxclv( -1, '30,  3 );	! filler class 1 ;
    auxclv( -1, '31,  0 );	! filler class 2 ;
    auxclv( -1, '16,  2 );	! output baud rate ;
    auxclv( -1, '17,  0 );	! no parity ;
    auxclv( -1, '20,  0 );	! full duplex ;
    auxclv( -1, '21,  3 );	! paramater A ;
    auxclv( -1, '22,  5 );	! paramater B ;
    auxclv( -1, '23,  8 );	! paramater C ;
    auxclv( -1, '14,  0 );	! compute delay on LF ;
  end;

simple procedure Set!Terminal!Type!J( integer In );
  begin
    if ( In ) then
      begin
	auxclv( -1, '15,  1 );	! input baud rate ;
	return;
      end;
    auxclv( -1, '24,  0 );	! host may send xon ;
    auxclv( -1, '27, 72 );	! width ;
    auxclv( -1, '30,  1 );	! filler class 1 ;
    auxclv( -1, '31,  1 );	! filler class 2 ;
    auxclv( -1, '16,  1 );	! output baud rate ;
    auxclv( -1, '17,  1 );	! even parity ;
    auxclv( -1, '20,  0 );	! full duplex ;
    auxclv( -1, '21,  6 );	! paramater A ;
    auxclv( -1, '22,  0 );	! paramater B ;
    auxclv( -1, '23,  2 );	! paramater C ;
    auxclv( -1, '14,  1 );	! compute delay on CR ;
  end;
! The "?" command ;

procedure Question!Command;
  begin
    ListOptions( "command", Primary!Options );
  end;
! The "HELP" command ;

procedure HELP!Command;
  begin
    print( "No help yet...  " );
    print( "Type '?' for a list of commands", crlf, crlf );
  end;
! The "A" command ;

  preload!with
	"?", "1", "2", "4", "8", "16", "32", "64", "128";
  string array A!Options[ 1 : 9 ];

procedure A!Command;
  begin
    string Token;
    if ( not MoreToScan ) then
      begin
	print( "A is set to ", A!Options[ auxclv( -1,-1,'21) + 2], crlf );
        return;
      end;
    Token _ TokenScan( NULL, false );
    if ( Token = NULL ) then return;
    case Match( Token, A!Options, false ) of
      begin
	[0]  OptionError( "A", Token );
	[1]  ListOptions( "A", A!Options );
        [2]  auxclv( -1, 0, '21 );
	[3]  auxclv( -1, 1, '21 );
	[4]  auxclv( -1, 2, '21 );
	[5]  auxclv( -1, 3, '21 );
	[6]  auxclv( -1, 4, '21 );
	[7]  auxclv( -1, 5, '21 );
	[8]  auxclv( -1, 6, '21 );
	[9]  auxclv( -1, 7, '21 )
      end;
  end;
! The "B" command ;

  preload!with
	"?", "0", "1", "2", "3", "4", "5", "6", "7",
	     "8", "9", "10", "12", "13", "14", "15";
  string array B!Options[ 1 : 16 ];

procedure B!Command;
  begin
    string Token;
    if ( not MoreToScan ) then
      begin
	print( "B is set to ", B!Options[ auxclv( -1,-1,'22 ) + 2], crlf );
	return;
      end;
    Token _ TokenScan( NULL, false );
    if ( Token = NULL ) then return;
    case Match( Token, B!Options, false ) of
      begin
	[0]  OptionError( "B", Token );
	[1]  ListOptions( "B", B!Options );
	[2]  auxclv( -1, 0, '22 );
	[3]  auxclv( -1, 1, '22 );
	[4]  auxclv( -1, 2, '22 );
	[5]  auxclv( -1, 3, '22 );
	[6]  auxclv( -1, 4, '22 );
	[7]  auxclv( -1, 5, '22 );
	[8]  auxclv( -1, 6, '22 );
	[9]  auxclv( -1, 7, '22 );
	[10] auxclv( -1, 8, '22 );
	[11] auxclv( -1, 9, '22 );
	[12] auxclv( -1,10, '22 );
	[13] auxclv( -1,11, '22 );
	[14] auxclv( -1,12, '22 );
	[15] auxclv( -1,13, '22 );
	[16] auxclv( -1,14, '22 );
	[17] auxclv( -1,15, '22 )
      end;
  end;
! The "C" command ;

  preload!with
	"?", "0", "1", "2", "3", "4", "5", "7", "10", "14",
	"19", "26", "36", "50", "69", "95", "131";
  string array C!Options[ 1 : 17 ];

procedure C!Command;
  begin
    string Token;
    if ( not MoreToScan ) then
      begin
	print( "C is set to ", C!Options[ auxclv(-1,-1,'23) + 2], crlf );
	return;
      end;
    Token _ TokenScan( NULL, false );
    if ( Token = NULL ) then return;
    case Match( Token, C!Options, false ) of
      begin
	[0]  OptionError( "C", Token );
	[1]  ListOptions( "C", C!Options );
	[2]  auxclv( -1, 0, '23 );
	[3]  auxclv( -1, 1, '23 );
	[4]  auxclv( -1, 2, '23 );
	[5]  auxclv( -1, 3, '23 );
	[6]  auxclv( -1, 4, '23 );
	[7]  auxclv( -1, 5, '23 );
	[8]  auxclv( -1, 6, '23 );
	[9]  auxclv( -1, 7, '23 );
	[10] auxclv( -1, 8, '23 );
	[11] auxclv( -1, 9, '23 );
	[12] auxclv( -1, 10, '23 );
	[13] auxclv( -1, 11, '23 );
	[14] auxclv( -1, 12, '23 );
	[15] auxclv( -1, 13, '23 );
	[16] auxclv( -1, 14, '23 );
	[17] auxclv( -1, 15, '23 )
      end;
  end;
! The "Delay" command ;

  preload!with
	"?", "CR", "LF";
  string array Delay!Options[ 1 : 3 ];

procedure DELAY!Command;
  begin
    string Token;
    if ( not MoreToScan ) then
      begin
	print( "DELAY is computed on ",
		case auxclv(-1,-1,'14) of ("LF","CR"), crlf );
	return;
      end;
    Token _ TokenScan( NULL, false );
    if ( Token = NULL ) then return;
    case Match( Token, DELAY!Options, false ) of
      begin
	[0]  OptionError( "DELAY", Token );
	[1]  ListOptions( "DELAY", DELAY!Options );
	[2]  auxclv( -1, 1, '14 );
	[3]  auxclv( -1, 0, '14 )
      end;
  end;
! The "CRLF" command ;

  preload!with
	"?", "ON", "OFF";
  string array CRLF!Options[ 1 : 3 ];

procedure CRLF!Command;
  begin
    string Token;
    if ( not MoreToScan ) then
      begin
	print( "CRLF is currently " );
        if ( (GETLCH land !Bit(10)) = 0 ) then
	  print( "ON", crlf )
	else
	  print( "OFF", crlf );
	return;
      end;
    Token _ TokenScan( NULL, false );
    if ( Token = NULL ) then return;
    case Match( Token, CRLF!Options, false ) of
      begin
	[0]  OptionError( "CRLF", Token );
	[1]  ListOptions( "CRLF", CRLF!Options );
	[2]  SETLCH( getlch land ( -1 xor !bit(10) ));
	[3]  SETLCH( getlch lor !bit(10) )
      end;
  end;
! The "EXPLAIN" command (something really *should* be done with this). ;

procedure EXPLAIN!Command;
  begin
   print( "For an explanation of TIO, see (DOC)TIO.DOC",crlf,crlf );
   FlushCommand;
  end;
! The "FULLDUPLEX" command ;

procedure FULLDUPLEX!Command;
  begin
    auxclv( -1, 0, '20 );	! clear the half duplex bit ;
  end;
! The "FILLER" command ;

  preload!with
	"?", "0", "1", "2", "3";
  string array FILLER!Options[ 1 : 5 ];

procedure FILLER!Command;
  begin
    string Token;
    if ( not MoreToScan ) then
      begin
	print( "TABFILL is ", auxclv( -1,-1,'31 ), ", " );
	print( "FFILL is ", auxclv( -1,-1,'30 ), crlf );
	return;
      end;
    Token _ TokenScan( "FILLER setting? ", true );
    if ( Token = NULL ) then return;
    case Match( Token, FILLER!Options, false ) of
      begin
	[0]  OptionError( "FILLER", Token );
	[1]  ListOptions( "FILLER", FILLER!Options );
	[2]  begin auxclv( -1, 0, '30 ); auxclv( -1, 0, '31 ); end;
	[3]  begin auxclv( -1, 1, '30 ); auxclv( -1, 1, '31 ); end;
	[4]  begin auxclv( -1, 2, '30 ); auxclv( -1, 2, '31 ); end;
	[5]  begin auxclv( -1, 3, '30 ); auxclv( -1, 3, '31 ); end
      end;
  end;
! The "FFILL" command (filler on form feeds) ;

  preload!with
	"?", "0", "1", "2", "3";
  string array FFILL!Options[ 1 : 5 ];

procedure FFILL!Command;
  begin
    string Token;
    if ( not MoreToScan ) then
      begin
	print( "FFILL is ", auxclv( -1, -1, '30 ), crlf );
	return;
      end;
    Token _ TokenScan( NULL, false );
    if ( Token = NULL ) then return;
    case Match( Token, FFILL!Options, false ) of
      begin
	[0]  OptionError( "FFILL", Token );
	[1]  ListOptions( "FFILL", FFILL!Options );
	[2]  auxclv( -1, 0, '30 );
	[3]  auxclv( -1, 1, '30 );
	[4]  auxclv( -1, 2, '30 );
	[5]  auxclv( -1, 3, '30 )
      end;
  end;
! The "HALFDUPLEX" command ;

procedure HALFDUPLEX!Command;
  begin
    auxclv( -1, 1, '20 );	! set the half duplex bit ;
  end;
! The "ID" command ;

  preload!with
	"?", "A", "B", "C", "D", "E", "F", "G", "J",
	"110", "150", "300", "400", "600", "1200",
	"2400", "4800", "9600";
  string array ID!Options[ 1 : 18 ];

procedure ID!Command;
  begin
    string Token1, Token2;
    if ( not MoreToScan ) then
      begin
	print( "Hm... this needs to be fixed", crlf );
	return;
      end;
    Token1 _ TokenScan( NULL, false );
    if ( Token1 = NULL ) then return;
    case Match( Token1, ID!Options, false ) of
      begin
	[0]  begin OptionError( "ID", Token1 ); return; end;
	[1]  begin ListOptions( "ID", ID!Options ); return; end;
	[2]  Set!Terminal!Type!A( 1 );
	[3]  Set!Terminal!Type!B( 1 );
	[4]  Set!Terminal!Type!C( 1 );
	[5]  Set!Terminal!Type!D( 1 );
	[6]  Set!Terminal!Type!E( 1 );
	[7]  Set!Terminal!Type!F( 1 );
	[8]  Set!Terminal!Type!G( 1 );
	[9]  Set!Terminal!Type!J( 1 );
	[10]
	[11]
	[12]
	[13]
	[14]
	[15]
	[16]
	[17] Set!Terminal!Type!1200( 1 );
	[18] Set!Terminal!Type!2400( 1 );
	[19] Set!Terminal!Type!4800( 1 );
	[20] Set!Terminal!Type!9600( 1 )
      end;
    if ( not MoreToScan ) then
      Token2 _ Token1
    else
      Token2 _ TokenScan( NULL, false );
    case Match( Token2, ID!Options, false ) of
      begin
	[0]  OptionError( "ID", Token2 );
	[1]  ListOptions( "ID", ID!Options );
	[2]  Set!Terminal!Type!A( 0 );
	[3]  Set!Terminal!Type!B( 0 );
	[4]  Set!Terminal!Type!C( 0 );
	[5]  Set!Terminal!Type!D( 0 );
	[6]  Set!Terminal!Type!E( 0 );
	[7]  Set!Terminal!Type!F( 0 );
	[8]  Set!Terminal!Type!G( 0 );
	[9]  Set!Terminal!Type!J( 0 );
	[10]
	[11]
	[12]
	[13]
	[14]
	[15]
	[16]
	[17] Set!Terminal!Type!1200( 0 );
	[18] Set!Terminal!Type!2400( 0 );
	[19] Set!Terminal!Type!4800( 0 );
	[20] Set!Terminal!Type!9600( 0 )
      end;
  end;
! The "TABFILL" command ;

  preload!with
	"?", "0", "1", "2", "3";
  string array TABFILL!Options[ 1 : 5 ];

procedure TABFILL!Command;
  begin
    string Token;
    if ( not MoreToScan ) then
      begin
	print( "TABFILL is ", auxclv( -1,-1,'31 ), crlf );
	return;
      end;
    Token _ TokenScan( NULL, false );
    if ( Token = NULL ) then return;
    case Match( Token, TABFILL!Options, false ) of
      begin
	[0]  OptionError( "TABFILL", Token );
	[1]  ListOptions( "TABFILL", TABFILL!Options );
	[2]  auxclv( -1, 0, '31 );
	[3]  auxclv( -1, 1, '31 );
	[4]  auxclv( -1, 2, '31 );
	[5]  auxclv( -1, 3, '31 )
      end;
  end;
! The 'TERMINAL' command ;

  preload!with
    "?",
    "CRT", "TYM125", "TYM225", "TYM315", "TYM325",
    "TTY37",
    "TYM300", "TYM301", "TYM310", "TYM311", "DCT500",
    "TTY33", "TTY35", "TYM200", "TYM201",
    "TYM100", "TYM110", "TYM211", "TYM212", "TYM213", "TYM221",
    "TI725", "TI733", "TI735", "NCR260", "EXECUPORT",
    "UNIVAC",
    "MEMOREX", "TERMINET360";
  string array TERMINAL!Options[ 1 : 30 ];

procedure TERMINAL!Command;
  begin
    string Token;
    integer Index;
    if ( not MoreToScan ) then
      begin
	print( "Sorry, can't tell you what your terminal is now", crlf );
	return;
      end;
    Token _ TokenScan( NULL, false );
    if ( Token = NULL ) then return;
    Index _ Match( Token, TERMINAL!Options, true );
    if ( Index < 0 ) then
      Index _ 0;
    case Index of
      begin
	[0]  OptionError( "TERMINAL", Token );
	[1]  ListOptions( "TERMINAL", TERMINAL!Options );
	[2][3][4][5][6]
	     begin end;
	[7]
	     begin end;
	[8][9][10][11][12]
	     begin end;
	[13][14][15][16]
	     begin end;
	[17][18][19][20][21][22][23][24][25][26][27]
	     begin end;
	[28]
	     begin end;
	[29][30]
	     begin end
      end;
  end;
! The "WIDTH" command ;

! Note:  This routine has to do some ad hoc parsing ;

define MinWidth = 20, MaxWidth = 200;

procedure WIDTH!Command;
  begin
    string Token;
    integer width;
    if ( not MoreToScan ) then
      begin
	print( "WIDTH is ", auxclv( -1, -1, '27 ), crlf );
	return;
      end;
    Token _ TokenScan( NULL, false );
    if ( Token = NULL ) then return;
    if ( Token = "?" ) then
      print( "The valid option for WIDTH is an integer between ",
	      MinWidth, " and ", MaxWidth, ".", crlf )
    else
      begin
  	width _ cvd( Token );
	if ( width < MinWidth or width > MaxWidth ) then
	  begin
	    print( "? '",Token,"' isn't a valid width", crlf );
	    return;
	  end
	else
	  auxclv( -1, width, '27 );	! set width ;
      end;
  end;
! The "WHAT" command ;

procedure WHAT!Command;
  begin
    integer I,J;
    boolean B;

    I _ auxclv( -1, -1, '20 );
    print( "Terminal is in ", case I of ("FULLDUPLEX","HALFDUPLEX"),
	   " mode.  " );

    print( "Terminal type is ", auxclv( -1, -1, '56 ), ".", crlf );
 
    I _ auxclv( -1, -1, '77 );
    print( "Backspace is", case I of (" not "," "), "treated as a delete.  " );
    print( "(BSD is ", case I of ("OFF","ON"), ").", crlf );
    I _ auxclv( -1, -1, '62 );
    print( "Deleted characters are ", case I of ("echoed","erased"),".  " );
    print( "(ERASE is ", case I of ("OFF","ON"), ").", crlf );

    print( "Input BAUD rate is ", case auxclv( -1, -1, '15 ) of
	   ("AUX","150","300","400","600","1200","300","150",
	    "?10","?11","2400","9600","?14","?15","?16","?17") );

    print( ", output BAUD rate is ", case auxclv( -1, -1, '16 ) of
	   ("AUX","150","300","400","600","1200","300","150",
	    "?10","?11","2400","9600","?14","?15","?16","?17" ),
	   ".", crlf );

    print( "XON is ", case auxclv( -1, -1, '24 ) of ("ON","OFF"), ".  " );
    I _ auxclv( -1, -1, '55 );
    print( "REVERSEXON is ", case I of ("disabled","enabled"), ".", crlf );

    B _ (GETLCH land !Bit(10)) = 0;
    print( "CRLF is " );
    if ( B ) then
      print( "ON, " )
    else
      print( "OFF, " );
    I _ auxclv( -1, -1, '27 );
    print( "WIDTH is ", I, ".", crlf );

    I _ auxclv( -1, -1, '14 );
    print( "DELAY computed on ", case I of ("LF","CR") );
    I _ auxclv( -1, -1, '21 );
    print( ",  A=", A!Options[ I+2 ] );
    I _ auxclv( -1, -1, '22 );
    print( ", B=", B!Options[ I+2 ] );
    I _ auxclv( -1, -1, '23 );
    print( ", C=", C!Options[ I+2 ], ".", crlf );

    I _ auxclv( -1, -1, '31 );
    print( "TABFILL is ", I );
    I _ auxclv( -1, -1, '30 );
    print( ", FFILL is ", I, ".", crlf );

    print( crlf );

  end;
 ! Here we are at the top level.  Nice view, eh? ;


  string Token;
  integer CmdNum;

  SetupTokenScanner;

  print( "TIO v", cvos(Version!Number),".",cvos(Edit!Number), crlf );

  while true do
    begin
      auxclv( -1, -1, '12 );		! send a yellow ball first ;
      Token _ TokenScan( ":", true );
      if ( equ( Token, NULL )) then continue;
      if (( CmdNum _ Match( Token, Primary!Options, true )) < 0 ) then
	begin
          print( "? '", Token, "' is ambiguous.  " );
	  print( "Type '?' for a list of commands.", crlf,crlf );
	  FlushCommand;
	end
      else if ( CmdNum = 0 ) then
	begin
	  print( "? '",Token,"' is not a known command.  " );
	  print( "Type '?' for a list of commands.",crlf );
	  FlushCommand;
	end
      else
	case CmdNum of
	  begin
	    [1]  Question!Command;
	    [2]  Help!Command;
	    [3]  DELAY!Command;
	    [4]  A!Command;
	    [5]  B!Command;
	    [6]  C!Command;
	    [7]  CRLF!Command;
	    [8]  EXPLAIN!Command;
	    [9]  FULLDUPLEX!Command;		! actually FDX ;
	    [10] FULLDUPLEX!Command;
	    [11] FILLER!Command;
	    [12] FFILL!Command;
	    [13] HALFDUPLEX!Command;		! actually HDX ;
	    [14] HALFDUPLEX!Command;
	    [15] ID!Command;
	    [16] TABFILL!Command;
	    [17] TERMINAL!Command;
	    [18] WIDTH!Command;
	    [19] WHAT!Command;
	    [20] Done
	  end;

      if ( MoreToScan ) then
	begin
	  print( "% '",Cmd,"' at end of command ignored.",crlf,crlf );
	  Cmd _ Null;
	end;
    end;

  start!code  calli '1,'12;  end;

end "TIO version 6" .
 $ I